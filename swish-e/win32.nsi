; Generated NSIS script file (generated by makensitemplate.phtml 0.20)
; by 192.168.2.1 on Nov 16 01 @ 12:13

; NOTE: this NSI script is designed for NSIS v1.3+

Name "SWISH-E 2.2"
OutFile "InstallSWISH-E.exe"

; Some default compiler settings (uncomment and change at will):
SetCompress auto ; (can be off or force)
SetDatablockOptimize on ; (can be off)
CRCCheck on ; (can be off)
AutoCloseWindow false ; (can be true for the window go away automatically at end)
ShowInstDetails show ; (can be show to have them shown, or nevershow to disable)
SetDateSave on ; (can be on to have files restored to their orginal date)

BrandingText "SWISH-E 2.2.1 Installation"
CompletedText "Installation Complete. Please see the SWISH-E Start Menu Group."
; Put up one of those silly blue backgrounds while installing
BGGradient 0000ff 000000 ffffff

LicenseText "You may redistribute SWISH-E under the following terms:"
LicenseData "COPYING"
EnabledBitmap src\win32\swish2-16.bmp
DisabledBitmap src\win32\swish2-16-desel.bmp
InstallDir "$PROGRAMFILES\SWISH-E"
InstallDirRegKey HKEY_LOCAL_MACHINE "SOFTWARE\SWISH-E Team\SWISH-E" ""
DirShow show ; (make this hide to not let the user change it)
DirText "Select location where to install SWISH-E:"

InstType "Full"

ComponentText "Which components do you require?"

Section "Program" ; (default, required section)

SetOutPath "$INSTDIR"
File COPYING

; SWISH-E Executable, static, and dynamic libraries
File src\win32\*.exe
File src\win32\vars.bat
; File src\win32\*.lib
; File src\Win32\*.dll

; Expat, LibXML2, and ZLib
File ..\expat\bin\*.dll
File ..\libxml2\win32\dsp\libxml2\*.dll
File ..\zlib\zlib.dll
File ..\pcre\bin\pcre.dll

; Microsoft Visual Studio 7 Runtime
File "C:\Program Files\Common Files\Microsoft Shared\VSA\7.0\VsaEnv\msvcr70.dll"

; Misc SWISH-E Support Files
File src\swishspider
File /r filter-bin
File /r prog-bin
File /r filters

; Rename a bunch of text files so Windows has a clue
Rename "$INSTDIR\COPYING" "$INSTDIR\COPYING.txt"
; Rename "$INSTDIR\swishspider" "$INSTDIR\swishspider.pl"
Rename "$INSTDIR\filter-bin\README" "$INSTDIR\filter-bin\README.txt"
Rename "$INSTDIR\prog-bin\README" "$INSTDIR\prog-bin\README.txt"

; Create shorcuts on the Start Menu
SetOutPath "$INSTDIR\"
CreateShortcut "$SMPROGRAMS\SWISH-E\SWISH-E Command Prompt.lnk" "%comspec%" "/K vars.bat"
SetOutPath "$SMPROGRAMS\SWISH-E\"
CreateShortcut "$SMPROGRAMS\SWISH-E\Browse Files.lnk" "$INSTDIR\"
WriteINIStr "$SMPROGRAMS\SWISH-E\Website.url" "InternetShortcut" "URL" "http://swish-e.org/"
CreateShortcut "$SMPROGRAMS\SWISH-E\License.lnk" "$INSTDIR\COPYING.txt"
SetOutPath "$SMPROGRAMS\SWISH-E\PERL_Resources"
WriteINIStr "$SMPROGRAMS\SWISH-E\PERL_Resources\Install_ActivePerl.url" "InternetShortcut" "URL" "http://www.activestate.com/Products/Download/Download.plex?id=ActivePerl"
WriteINIStr "$SMPROGRAMS\SWISH-E\PERL_Resources\CPAN_PERL_Modules.url" "InternetShortcut" "URL" "http://search.cpan.org/"
SectionEnd ; end of default section

Section "Documentation"
SectionIn 1
SetOutPath "$INSTDIR"
RMDIR /r "$INSTDIR\html"
File /r html

; Create shorcuts on the Start Menu
SetOutPath "$SMPROGRAMS\SWISH-E\"
WriteINIStr "$SMPROGRAMS\SWISH-E\Documentation.url" "InternetShortcut" "URL" "file://$INSTDIR\html\index.html"
WriteINIStr "$SMPROGRAMS\SWISH-E\Questions_and_Troubleshooting.url" "InternetShortcut" "URL" "file://$INSTDIR\html\INSTALL.html#QUESTIONS_AND_TROUBLESHOOTING"
SectionEnd ; end of section 'Documentation'

Section "Examples"
SectionIn 1
SetOutPath "$INSTDIR"
RMDIR /r "$INSTDIR\example"
RMDIR /r "$INSTDIR\conf"
File /r example
File /r conf

; Rename text files so Windows has a clue
Rename "$INSTDIR\conf\README" "$INSTDIR\conf\README.txt"
Rename "$INSTDIR\example\README" "$INSTDIR\example\README.txt"
SectionEnd ; end of section 'Examples'

Section "-post" ; (post install section, happens last after any optional sections)
; add any commands that need to happen after any optional sections here
WriteRegStr HKEY_LOCAL_MACHINE "SOFTWARE\SWISH-E Team\SWISH-E" "" "$INSTDIR"
WriteRegStr HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\SWISH-E" "DisplayName" "SWISH-E (remove only)"
WriteRegStr HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\SWISH-E" "UninstallString" '"$INSTDIR\uninst.exe"'

WriteUninstaller "uninst.exe"

MessageBox MB_OK "SWISH-E has been installed to $INSTDIR. Please see the SWISH-E Start Menu group."

SectionEnd ; end of -post section



Section Uninstall
; add delete commands to delete whatever files/registry keys/etc you installed here.
; UninstallText "This will remove SWISH-E from your system"
Delete "$INSTDIR\uninst.exe"
DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\SWISH-E Team\SWISH-E"
DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\SWISH-E"
RMDir /r "$INSTDIR"
RMDir /r "$SMPROGRAMS\SWISH-E\"
SectionEnd ; end of uninstall section

; eof
